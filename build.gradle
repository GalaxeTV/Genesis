plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'tv.galaxe'
version = '1.0.1'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://maven.enginehub.org/repo/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.9"
    compileOnly group: 'net.luckperms', name: 'api', version: '5.4'
    implementation "dev.triumphteam:triumph-gui:3.1.7"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

spotless {
    ratchetFrom 'origin/main'

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    java {
        importOrder()
        removeUnusedImports()
        cleanthat()
        eclipse()
        toggleOffOn()
        formatAnnotations()
    }
}
